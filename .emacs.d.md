
# ~/.emacs.d

- [General configuration](#general)
  - [User interface](#user-interface)
  - [Backups](#backups)
  - [History](#history)
  - [Native compilation](#general-native-compilation)
  - [Summary](#general-summary)
- [Package management](#package-management)
  - [Straight.el](#straight.el)
    - [Installation](#straight.el-installation)
    - [Usage](#straight.el-usage)
  - [Use-package](#use-package)
    - [Installation](#use-package-installation)
    - [Using straight.el with use-package](#straight.el-use-package)
    - [Usage](#use-package-usage)
  - [Summary](#package-management-summary)
- [Linguist](#linguist)
  - [Language packs](#orgc84a3b1)
    - [[dockerfile-mode](https://github.com/spotify/dockerfile-mode)](#org8534e40)
    - [[elixir-mode](https://github.com/elixir-editors/emacs-elixir)](#org9bd7375)
    - [[gitignore-mode](https://github.com/magit/git-modes/blob/main/gitignore-mode.el)](#orgf1c35a5)
    - [[ledger-mode](https://github.com/ledger/ledger-mode)](#org326290f)
    - [[markdown-mode](https://github.com/jrblevin/markdown-mode)](#org6c0ed3b)
    - [[yaml-mode](https://github.com/yoshiki/yaml-mode)](#org9f8870f)
- [Evil](#evil)
  - ["Fix" the tab key for visibility cycling in Org and Evil mode](#evil-tab)
- [Org mode](#org-mode)
  - [Org-agenda](#org96c66fc)
  - [Org-babel](#orge0c3858)
  - [Org-auto-tangle](#org-auto-tangle)
  - [Generate Github-flavored Markdown READMEs from Org files](#generate-readme)
    - [Generating GitHub-flavored Markdown](#orgebbb0c8)
    - [Symlinking as README.md](#org866edf6)
- [Org-roam](#org-roam)
  - [Sync automatically](#orge6b21e1)
  - [Capture templates](#org33361d6)
- [Clipboard](#orgb4e2af3)



<a id="general"></a>

## General configuration

The first part of the configuration file configures Emacs' main functionality. Since this doesn't require any of the plugins to load, it's placed in the `early-init.el` configuration file.

Loading it first has some advantages. For example, it prevents the hidden menu bar from flickering on startup.


<a id="user-interface"></a>

### User interface

By default, Emacs shows the menu bar at the top of the screen, which is accessed by pressing `<F10>`. To turn it off, set `menu-bar-mode` to a negative number:

```emacs-lisp
;; Hide the top menu bar
(menu-bar-mode -1)
```

On startup, Emacs shows the startup screen, which links to the manual and the tutorial. To open with a blank screen instead, set

```emacs-lisp
;; Disable the startup screen
(setq inhibit-startup-screen t)
```

Emacs also prints a message in the echo area that explains how to read more information about Emacs and the GNU system:

```
For information about GNU Emacs and the GNU system, type C-h C-a.
```

To prevent this message from being printed, use `inihibit-startup-echo-area-message`. Emacs tries to prevent this message from being disabled by requiring the value to match the current user's name. This can be circumvented by using `user-login-name`, but only after setting the `saved-value` property on the `inhibit-startup-echo-area-message` variable:

```emacs-lisp
(put 'inhibit-startup-echo-area-message 'saved-value t)
(setq inhibit-startup-echo-area-message (user-login-name))
```


<a id="backups"></a>

### Backups

Instead of placing backup files (like `init.el~`) in the original file's directory, store them in `~/.emacs.d/backups/`:

```emacs-lisp
;; Store all backup files in =~/.emacs.d/backups/
(setq backup-directory-alist '(("." . "~/.emacs.d/backups")))
```

The `backup-directory-alist` lists cons cells with a regular expression and a directory name. In this setting, the regular expression is `"."`, meaning all files are stored in the specified directory.


<a id="history"></a>

### History

Commands executed through `M-x` are remembered to suggest them when pressing `M-x` again later. To execute a previously used command again, press `UP` in the minibuffer after pressing `M-x`.

Because the history is not saved, starting a new Emacs session loses all previously executed commands. To configure Emacs to periodically save the minibuffer history to a file and load it when starting a new session, enable `savehist-mode`:

```emacs-lisp
;; Save minibuffer history
(savehist-mode 1)
```

By default, Emacs saves the minibuffer history to `~/.emacs.d/history`.


<a id="general-native-compilation"></a>

### Native compilation

Emacs compiles packages natively if the `--with-native-compilation` configuration flag is set while [building Emacs from source](../notes/.emacs.d/emacs-from-source.md). Since this happens asynchronously, warnings about deprecations in the packages are printed whenever they happen in the compile process, popping open the `*Warnings` buffer. To hide these warnings, unset `native-comp-async-report-warnings-errors`:

```emacs-lisp
;; Hide native compilation warnings
(setq native-comp-async-report-warnings-errors nil)
```


<a id="general-summary"></a>

### Summary

The first part of the `init.el` file removes the menu bar and disables the startup screen and message:

```emacs-lisp
;; Hide the top menu bar
(menu-bar-mode -1)

;; Save minibuffer history
(savehist-mode 1)

;; Disable the startup message
(put 'inhibit-startup-echo-area-message 'saved-value t)

(setq
 ;; Disable the startup screen
 inhibit-startup-screen t
 ;; Disable the startup message
 inhibit-startup-echo-area-message (user-login-name)
 ;; Store all backup files in =~/.emacs.d/backups/
 backup-directory-alist '(("." . "~/.emacs.d/backups"))
 ;; Hide native compilation warnings
 native-comp-async-report-warnings-errors nil)
```


<a id="package-management"></a>

## Package management

Emacs includes a package manager named `package.el`, which installs packages from the official Emacs Lisp Package Archive, named [GNU ELPA.](https://elpa.gnu.org) GNU ELPA hosts a selection of packages, but most are available on [MELPA](https://melpa.org), which is an unofficial package archive that implements the ELPA specification. To use MELPA, it has to be [installed](https://melpa.org/#/getting-started) by adding it to the list of `package.el` package archives.

The built-in package manager installs packages through the `package-install` function. For example, to install the "evil-commentary" package from MELPA, call `package-install` inside Emacs:

`M-x` `package-install` `<RET>` `evil-commentary` `<RET>`


<a id="straight.el"></a>

### Straight.el

[Straight.el](https://github.com/radian-software/straight.el) is an alternative package manager that installs packages through Git checkouts instead of downloading tarballs from one of the package archives. Doing so allows installing forked packages, altering local package checkouts, and locking packages to exact versions for reproducable setups.


<a id="straight.el-installation"></a>

#### Installation

When on Emacs 29 with the `--with-native-compilation` turned on, make sure you're on straight.el's development branch, as staying on the main branch breaks its bootstrap for now (`(void-variable native-comp-deferred-compilation-deny-list)`):

```emacs-lisp
(setq straight-repository-branch "develop")
```

The [Getting started](https://github.com/radian-software/straight.el#getting-started) section in the straight.el README provides the bootstrap code to place inside `~/.emacs.d/init.el` in order to install it:

```emacs-lisp
;; Install straight.el
(defvar bootstrap-version)
(let ((bootstrap-file
       (expand-file-name "straight/repos/straight.el/bootstrap.el" user-emacs-directory))
      (bootstrap-version 6))
  (unless (file-exists-p bootstrap-file)
    (with-current-buffer
        (url-retrieve-synchronously
         "https://raw.githubusercontent.com/radian-software/straight.el/develop/install.el"
         'silent 'inhibit-cookies)
      (goto-char (point-max))
      (eval-print-last-sexp)))
  (load bootstrap-file nil 'nomessage))
```

Straight.el uses package archives like GNU ELPA as registries to find the linked repositories to clone from. Since these are checked automatically, there's no need to add them to the list of package archives.

While package.el loads all installed packages on startup, straight.el only loads packages that are referenced in the init file. This allows for installing packages temporarily without slowing down Emacs' startup time on subsequent startups.

To create a truly reproducable setup, disable package.el in favor of straight.el by turning off `package-enable-at-startup`. Because this step needs to happen before package.el gets a chance to load packages, it this configuration needs to be set in the early init file:

```emacs-lisp
;; Disable package.el in favor of straight.el
(setq package-enable-at-startup nil)
```

With this configuration set, Emacs will only load the packages installed through straight.el.


<a id="straight.el-usage"></a>

#### Usage

To use straight.el to install a package for the current session, execute the `straight-use-package` command:

`M-x` `straight-use-package` `<RET>` `evil-commentary` `<RET>`

To continue using the package in future sessions, add the `straight-use-package` call to `~/.emacs/init.el`:

```emacs-lisp
(straight-use-package 'evil-commentary)
```

To update an installed package, execute the `straight-pull-package` command:

`M-x` `straight-pull-package` `<RET>` `evil-commentary` `<RET>`

To update the version lockfile, which is used to target the exact version to check out when installing, run `straight-freeze-versions`:

`M-x` `straight-freeze-versions` `<RET>`


<a id="use-package"></a>

### Use-package

[Use-package](https://github.com/jwiegley/use-package) is a macro to configure and load packages in Emacs configurations. It interfaces with package managers like package.el or straight.el to install packages, but is not a package manager by itself.

For example, when using straight.el without use-package, installing and starting evil-commentary requires installing the package and starting it as two separate steps:

```emacs-lisp
(straight-use-package 'evil-commentary)
(evil-commentary-mode)
```

[Combined with use-package](#straight.el-use-package), the installation and configuration are unified into a single call to `use-package`:

```emacs-lisp
(use-package evil-commentary
  :config
  (evil-commentary-mode))
```

Aside from keeping configuration files tidy, having package configuration contained within a single call allows for more advanced package setups. For example, packages can be lazy-loaded, keeping their configuration code from executing until the package they configure is needed.


<a id="use-package-installation"></a>

#### Installation

To install use-package with straight.el, use `straight-use-package`:

```emacs-lisp
;; Install use-package
(straight-use-package 'use-package)
```


<a id="straight.el-use-package"></a>

#### Using straight.el with use-package

By default, use-package uses package.el to install packages. To use straight.el instead of package.el, pass the `:straight` option:

```emacs-lisp
(use-package evil-commentary
  :straight t)
```

To configure use-package to always use straight.el, use `use-package` to configure straight.el to turn on `straight-use-package-by-default`<sup><a id="fnr.1" class="footref" href="#fn.1" role="doc-backlink">1</a></sup>:

```emacs-lisp
;; Configure use-package to use straight.el by default
(use-package straight
  :custom
  (straight-use-package-by-default t))
```

Now, installing any package using use-package uses straight.el, even when omitting the `:straight` option.

Having both straight.el and use-package installed and configured to work together, the `straight-use-package` function isn't used anymore. Instead, all packages are installed and configured through use-package.


<a id="use-package-usage"></a>

#### Usage

Use the `use-package` macro to load a package. If the package is not installed yet, it is installed automatically:

```emacs-lisp
(use-package evil-commentary)
```

Use-package provides keywords to add configuration, key bindings and variables. Although there are [many more options](https://github.com/jwiegley/use-package#getting-started/), some examples include `:config`, `:init`, `:bind`, and `:custom`:

-   **`:config` and `:init`:** The `:config` and `:init` configuration keywords define code that's run right after, or right before a package is loaded, respectively.
    
    For example, call `evil-mode` from the `:config` keyword to start Evil after loading its package. To turn off `evil-want-C-i-jump` right before evil is loaded (instead of adding it to the early init file), configure it in the `:init` keyword:
    
    ```emacs-lisp
    (use-package evil
      :init
      (setq evil-want-C-i-jump nil)
      :config
      (evil-mode))
    ```

-   **`:bind`:** Adds key bindings after a module is loaded. For example, to use `consult-buffer` instead of the built-in `switch-to-buffer` after loading the consult package, add a binding through the `:bind` keyword:
    
    ```emacs-lisp
    (use-package consult
      :bin
      ("C-x b" . consult-buffer))
    ```

-   **`:custom`:** Sets customizable variables. The variables set through use-package are not saved in Emacs' custom file. Instead, all custom variables are expected to be set through use-package. In an example from before, the `:custom` keyword is used to set the `straight-use-package-by-default` configuration option after loading straight.el:
    
    ```emacs-lisp
    (use-package straight
      :custom
      (straight-use-package-by-default t))
    ```


<a id="package-management-summary"></a>

### Summary

The resulting `~/.emacs.d/init.el` file installs straight.el and use-package, and configures straight.el as the package manager for use-package to use:

```emacs-lisp
(setq straight-repository-branch "develop")

;; Install straight.el
(defvar bootstrap-version)
(let ((bootstrap-file
       (expand-file-name "straight/repos/straight.el/bootstrap.el" user-emacs-directory))
      (bootstrap-version 6))
  (unless (file-exists-p bootstrap-file)
    (with-current-buffer
        (url-retrieve-synchronously
         "https://raw.githubusercontent.com/radian-software/straight.el/develop/install.el"
         'silent 'inhibit-cookies)
      (goto-char (point-max))
      (eval-print-last-sexp)))
  (load bootstrap-file nil 'nomessage))

;; Install use-package
(straight-use-package 'use-package)

;; Configure use-package to use straight.el by default
(use-package straight
  :custom
  (straight-use-package-by-default t))
```

The `~/.emacs.d/early-init.el` file disables package.el to disable its auto-loading, causing all packages to be loaded through straight.el in the init file:

```emacs-lisp
;; Disable package.el in favor of straight.el
(setq package-enable-at-startup nil)
```

This is the only configuration set in the early init file. All other packages are installed and configured through use-package, which makes sure to load configuration options before packages are loaded, if configured with the `:init` keyword.


<a id="linguist"></a>

## Linguist

Linguist is a language modes pack for Emacs, inspired by [vim-polyglot](https://github.com/sheerun/vim-polyglot).


<a id="orgc84a3b1"></a>

### Language packs


<a id="org8534e40"></a>

#### [dockerfile-mode](https://github.com/spotify/dockerfile-mode)

```emacs-lisp
(use-package dockerfile-mode
  :config (linguist--loaded-message "dockerfile-mode")
  :mode ("\\.dockerfile\\'" . dockerfile-mode)
  :mode ("[/\\]\\(?:Containerfile\\|Dockerfile\\)\\(?:\\.[^/\\]*\\)?\\'" . dockerfile-mode))
```


<a id="org9bd7375"></a>

#### [elixir-mode](https://github.com/elixir-editors/emacs-elixir)

```emacs-lisp
(use-package elixir-mode
  :config (linguist--loaded-message "elixir-mode")
  :mode ("mix\\.lock" . elixir-mode)
  :mode ("\\.exs\\'" . elixir-mode)
  :mode ("\\.ex\\'" . elixir-mode)
  :mode ("\\.elixir\\'" . elixir-mode))
```


<a id="orgf1c35a5"></a>

#### [gitignore-mode](https://github.com/magit/git-modes/blob/main/gitignore-mode.el)

```emacs-lisp
(use-package git-modes
  :config (linguist--loaded-message "linguist-mode")
  :mode ("/git/ignore\\'" . gitignore-mode)
  :mode ("/info/exclude\\'" . gitignore-mode)
  :mode ("/\\.gitignore\\'" . gitignore-mode))
```


<a id="org326290f"></a>

#### [ledger-mode](https://github.com/ledger/ledger-mode)

```emacs-lisp
(use-package ledger-mode
  :config (linguist--loaded-message "ledger-mode")
  :mode ("\\.ledger\\'" . ledger-mode))
```


<a id="org6c0ed3b"></a>

#### [markdown-mode](https://github.com/jrblevin/markdown-mode)

```emacs-lisp
(use-package markdown-mode
  :config (linguist--loaded-message "markdown-mode")
  :mode ("\\.\\(?:md\\|markdown\\|mkd\\|mdown\\|mkdn\\|mdwn\\)\\'" . markdown-mode))
```


<a id="org9f8870f"></a>

#### [yaml-mode](https://github.com/yoshiki/yaml-mode)

```emacs-lisp
  (use-package yaml-mode
    :config (linguist--loaded-message "yaml-mode")
    :mode ("\\.\\(e?ya?\\|ra\\)ml\\'" . yaml-mode))

#+headers: :tangle init.el
#+headers: :exports none
#+begin_src emacs-lisp
  (load "~/.emacs.d/linguist.el" nil t)
```


<a id="evil"></a>

## Evil

[Evil](https://github.com/emacs-evil/evil) is a Vim emulator for emacs. Install it with `straight.el`, and turn `evil-mode` on:

```emacs-lisp
(use-package evil
  :config
  (evil-mode))
```


<a id="evil-tab"></a>

### "Fix" the tab key for visibility cycling in Org and Evil mode

Emacs' Org mode uses the `TAB` key to call `org-cycle`, which cycles visibility for headers. Every `TAB` press on a headline cycles through a different function<sup><a id="fnr.2" class="footref" href="#fn.2" role="doc-backlink">2</a></sup>:

1.  The first press folds the headline's subtree, showing only the headline itself
2.  The scond press shows the headline and its direct descendants, but keeps them folded
3.  The third press shows the headline's complete subtree

However, running Emacs with Evil mode in a terminal breaks the `TAB` key for cycling through header visibility in Org mode.

Most terminals map both `TAB` and `C-i` to `U+0009 (Character Tabulation)` for historical reasons, meaning they're recognised as the same keypress. Because of this, there is no way to map different functions to them inside Emacs.

Evil remaps `C-i` to `evil-jump-forward` to emulate Vim's jump lists feature<sup><a id="fnr.3" class="footref" href="#fn.3" role="doc-backlink">3</a></sup>, which overwrites the default mapping for the `TAB` key in Org mode.

To fix the tab key's functionality in Org mode, sacrifice Evil's `C-i` backward jumping by turning it off in your configuration with the `evil-want-C-i-jump` option.

This option needs to be set *before* Evil is loaded to take effect, so put it in the early init file:

```emacs-lisp
;; Disable C-i to jump forward to restore TAB functionality in Org mode.
(setq evil-want-C-i-jump nil)
```

With use-package, set the `evil-want-C-i-jump` before the package is loaded by using the `:init` keyword:

```emacs-lisp
;; Install Evil and disable C-i to jump forward to restore TAB functionality in Org mode.
(use-package evil
             :init (setq evil-want-C-i-jump nil)
             :config (evil-mode))
```


<a id="org-mode"></a>

## Org mode

Install Org mode to prevent version mismatches between the Org bundled with Emacs and the dependencies of other packages.

```emacs-lisp
(use-package org)
```


<a id="org96c66fc"></a>

### Org-agenda

Configure Org to recursively look through `~/notes/` for displaying the agenda:

```emacs-lisp
(use-package org
  :custom
  (org-agenda-files (directory-files-recursively "~/notes/" "\\.org$")))
```


<a id="orge0c3858"></a>

### Org-babel

[Org-babel](https://orgmode.org/worg/org-contrib/babel/) executes source code within Org documents. It only supports Emacs Lisp by default, but more languages can be added to `org-babel-load-languages`:

```emacs-lisp
(use-package org
  :custom
  (org-babel-load-languages '((emacs-lisp . t)
                              (shell . t))))
```


<a id="org-auto-tangle"></a>

### Org-auto-tangle

[Org-auto-tangle](https://github.com/yilkalargaw/org-auto-tangle) automatically tangles Org files on save.

Be default, it only auto-tangles files marked with the `#+auto_tangle` header option. By turning on `org-auto-tangle-default`, org-auto-tangle tangles all Org files, even without that option.

```emacs-lisp
(use-package org-auto-tangle
  :hook
  (org-mode . org-auto-tangle-mode)
  :custom
  (org-auto-tangle-default t))
```


<a id="generate-readme"></a>

### Generate Github-flavored Markdown READMEs from Org files

Although GitHub supports Org files to a certain extent<sup><a id="fnr.4" class="footref" href="#fn.4" role="doc-backlink">4</a></sup>, it seems to generally be a bad match. For projects hosted on the code platform, it makes sense to generate a README file in GitHub's own version of Markdown.


<a id="orgebbb0c8"></a>

#### Generating GitHub-flavored Markdown

We'll start out by installing the [ox-gfm](https://github.com/larstvei/ox-gfm) package, a GitHub-flavored Markdown exporter derived from [Org's `ox-md.el`](https://git.savannah.gnu.org/cgit/emacs/org-mode.git/tree/lisp):

```emacs-lisp
(use-package ox-gfm)
```

Then, since ox-md (and thus ox-gfm) [don't support document titles](https://lists.gnu.org/archive/html/emacs-orgmode/2017-08/msg00555.html), we'll install the `ox-md-title` extension, and it's dependency `ox-extend`<sup><a id="fnr.5" class="footref" href="#fn.5" role="doc-backlink">5</a></sup>:

```emacs-lisp
(use-package ox-extend
  :straight '(ox-extend
              :type git
              :host github
              :repo "jeffkreeftmeijer/ox-extend.el"))

(use-package ox-md-title
  :straight '(ox-md-title
              :type git
              :host github
              :repo "jeffkreeftmeijer/ox-md-title.el"))
```

To generate the Markdown file, call `org-publish-file` with `org-gfm-publish-to-gfm` as the `:publishing-function`. Add `ox-md-title` to the list of `:extensions`:

```emacs-lisp
(ox-extend-add)

(org-publish-file ".emacs.d.org"
                  '("readme"
                    :base-directory "."
                    :publishing-directory "."
                    :publishing-function org-gfm-publish-to-gfm
                    :extensions ('ox-md-title)))
```

This generates a Markdown version with the same name as the Org file, except for the extension.


<a id="org866edf6"></a>

#### Symlinking as README.md

GitHub expects repositories to have a `README.md` file. You could name your Org file `README.org` to make the Markdown exporter generate a file named `README.md`. If that's impossible or undesirable, you can create a symlink from `README.md` to your generated Markdown file:

```bash
ln -s .emacs.d.md README.md
```


<a id="org-roam"></a>

## Org-roam

[Org-roam](https://github.com/org-roam/org-roam) is a knowledge management system. Install it with `straight.el` and set the directory for notes to `~/notes`.

```emacs-lisp
;; Org-roam
(use-package org-roam
  :init
  (setq org-roam-directory (file-truename "~/notes")))
```


<a id="orge6b21e1"></a>

### Sync automatically

Turn on `org-roam-db-autosync-mode` to automatically sync the database instead of manually running `org-roam-db-sync`:

```emacs-lisp
;; Org-roam
(use-package org-roam
  :init
  (setq org-roam-directory (file-truename "~/notes"))
  :config
  (org-roam-db-autosync-mode))
```


<a id="org33361d6"></a>

### Capture templates

Org-roam's default capture template creates notes with filenames that include the current time, like `20230227171947-org-escape.org`. The time prefix is used to make sure note names are unique.

With some care not to duplicate names, the prefix can be removed by overwriting the default template<sup><a id="fnr.6" class="footref" href="#fn.6" role="doc-backlink">6</a></sup> in `org-roam-capture-templates`. To keep the creation date, this new default template also adds a date header attribute to the generated file contents.

Finally, the author name is added by calling `user-full-name`:

```emacs-lisp
;; Org-roam
(use-package org-roam
  :init
  (setq org-roam-directory (file-truename "~/notes"))
  :config
  (org-roam-db-autosync-mode)
  :custom
  (org-roam-capture-templates
   '(("d" "default" plain
      "%?"
      :target (file+head "${slug}.org" "#+title: ${title}\n#+author: %(user-full-name)\n#+date: %<%Y-%m-%d>")
      :unnarrowed t))))
```


<a id="orgb4e2af3"></a>

## Clipboard

Use the macOS system clipboard through `pbpaste=/=pbcopy` by using [xclip](https://elpa.gnu.org/packages/xclip.html):

```emacs-lisp
(use-package xclip
  :init
  (xclip-mode))
```

## Footnotes

<sup><a id="fn.1" class="footnum" href="#fnr.1">1</a></sup> Calling `use-package` would normally install straight.el, but since it's already installed, the installation is skipped and the configuration is set. Here, the call to `use-package` is only used to configure straight.el, by setting the `straight-use-package-by-default` option.

<sup><a id="fn.2" class="footnum" href="#fnr.2">2</a></sup> Vim and Evil don't have a direct equivalent to fold cycling, but they have three different commands that achieve the same result:

1.  `zf` folds the headline's subtree
2.  `zo` opens the headline's subtree to show its direct descendants
3.  `zO` opens the complete subtree

<sup><a id="fn.3" class="footnum" href="#fnr.3">3</a></sup> Vim and Evil remember jumps between lines and files in the "jump list". Because the jump locations are stored, you can use `C-o` to jump to a previous location, and `C-i` to jump back. For example:

1.  To move to the next empty line, press `}` in normal mode
2.  To jump back to where you came from, press `C-o`
3.  To jump back to that empty line, press `C-i`

<sup><a id="fn.4" class="footnum" href="#fnr.4">4</a></sup> Aside from the obvious things like not executing code samples and not supporting noweb, GitHub's Org support doesn't understand the `:exports none` header line, inline-highlighted code (`src_lisp{ (message "rhubarb") }`), or example blocks.

<sup><a id="fn.5" class="footnum" href="#fnr.5">5</a></sup> : These packages aren't available on any Emacs package archive yet, so we'll install them by hand for now.

<sup><a id="fn.6" class="footnum" href="#fnr.6">6</a></sup> Org-roam's default capture template:

```emacs-lisp
(("d" "default" plain
  "%?"
  :target (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}")
  :unnarrowed t))
```